apply plugin: 'com.android.application'

repositories{
    maven {
        url 'http://dl.bintray.com/amulyakhare/maven'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.wordcamp"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0-beta-1"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "LIST_WORDCAMP_URL", '"' + project.properties["default.list_wordcamp_url"] + '"'
        }

        debug {
            debuggable true
            buildConfigField "String", "LIST_WORDCAMP_URL", '"' + project.properties["default.list_wordcamp_url"] + '"'
        }

        localdebug.initWith(buildTypes.debug);
        localdebug {
            buildConfigField "String", "LIST_WORDCAMP_URL", '"' + project.properties["local.list_wordcamp_url"] + '"'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.loopj.android:android-async-http:1.4.7'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'se.emilsjolander:stickylistheaders:2.6.0'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
}

android.buildTypes.all { buildType ->
    project.properties.any { property ->
        if (property.key.toLowerCase().startsWith("wc.")) {
            buildType.buildConfigField "String", property.key.replace("wc.", "").replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
    }
}
